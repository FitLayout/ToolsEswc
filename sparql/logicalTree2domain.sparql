PREFIX b: <http://fitlayout.github.io/ontology/render.owl#>
PREFIX a: <http://fitlayout.github.io/ontology/segmentation.owl#>

PREFIX dc: <http://purl.org/dc/elements/1.1/#>
PREFIX swrc: <http://swrc.ontoware.org/ontology/#>
PREFIX bibo: <http://purl.org/ontology/bibo/#>
PREFIX swc: <http://data.semanticweb.org/ns/swc/ontology/#>
PREFIX timeline: <http://purl.org/NET/c4dm/timeline.owl#>
PREFIX dcterms: <http://dublincore.org/documents/dcmi-terms/#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dbpedia: <http://dbpedia.org/ontology/#>

CONSTRUCT
{ 
    ?workshop a swc:WorkshopEvent .
    ?workshop dc:title ?workshopTitle .
    ?workshop bibo:shortTitle ?workshopShortTitle.
    ?workshop swc:subEventOf ?conference .
    ?workshop timeline:beginsAtDatetime ?startdatedt .
    ?workshop timeline:endsAtDatetime ?enddatedt .
    ?workshop swc:hasLocation ?vcountry.
  	
  
    ?conference a swc:ConferenceEvent .
  	?conference dc:title ?colocatedTitle .
  
    ?proceedings a swc:Proceedings .
    ?proceedings bibo:presentedAt ?workshop.
    
    
    ?paper a swc:Paper .
    ?paper dc:title ?paperTitle .
	  ?paper swc:partOf ?proceedings .
    ?paper swc:partOf ?workshop .
    
    ?proceedings swc:hasPart ?paper .
    ?workshop swc:hasPart ?paper .
    
	  ?paper swrc:author ?author .
    ?author rdf:type foaf:Person .
    ?author foaf:name ?authorName .
    ?author foaf:made ?paper .           
    
    ?paper bibo:section ?sectionName .
    
    ?proceedings swrc:editor ?editor .            
    ?editor rdf:type foaf:Person .
    ?editor foaf:name ?editorName .
    ?editor swrc:affiliation ?eaffilName .
    ?editor bibo:place ?ecountry .  
    
}
WHERE {

  
    ?workshopPart a a:LogicalArea .
  	?workshopPart a:hasTag a:tag-ESWC--root .
  	?workshopPart a:hasText ?workshopUri .
  	
    ?avtitle a:isSubordinateTo ?workshopPart .
  	?avtitle a:hasTag a:tag-ESWC--vtitle .
  	?avtitle a:hasText ?workshopTitle .
    
    ?vshortTitle a:isSubordinateTo ?workshopPart .
  	?vshortTitle a:hasTag a:tag-ESWC--vshort .
  	?vshortTitle a:hasText ?workshopShortTitle .
  
  OPTIONAL {  
    ?colocated a:isSubordinateTo ?workshopPart .
  	?colocated a:hasTag a:tag-ESWC--colocated .
  	?colocated a:hasText ?colocatedTitle .
  }
  
    ?startdatePart a:isSubordinateTo ?workshopPart .
  	?startdatePart a:hasTag a:tag-ESWC--startdate .
  	?startdatePart a:hasText ?startdate .
    
    ?enddatePart a:isSubordinateTo ?workshopPart .
  	?enddatePart a:hasTag a:tag-ESWC--enddate .
  	?enddatePart a:hasText ?enddate .
    
    ?vcountryPart a:isSubordinateTo ?workshopPart .
  	?vcountryPart a:hasTag a:tag-ESWC--vcountry .
  	?vcountryPart a:hasText ?vcountryText .
    

    ?apaper a:isSubordinateTo ?workshopPart .
  	?apaper a:hasTag a:tag-ESWC--paper .
  	?apaper a:hasText ?paperUri .
    
  	?paperTitlePart a:isSubordinateTo ?apaper .
    ?paperTitlePart a:hasTag a:tag-ESWC--title .
  	?paperTitlePart a:hasText ?paperTitle .
    
  	?paperAuthorPart a:isSubordinateTo ?apaper .
    ?paperAuthorPart a:hasTag a:tag-ESWC--authors .
  	?paperAuthorPart a:hasText ?authorName .
  
  OPTIONAL {
    ?paperSectionPart a:isSubordinateTo ?apaper .
    ?paperSectionPart a:hasTag a:tag-ESWC--section .
  	?paperSectionPart a:hasText ?sectionName .
  }
  	?editorPart a:isSubordinateTo ?workshopPart .
    ?editorPart a:hasTag a:tag-ESWC--veditor .
  	?editorPart a:hasText ?editorName .
  
  OPTIONAL {
    ?eaffilPart a:isSubordinateTo ?editorPart .                 
    ?eaffilPart a:hasTag a:tag-ESWC--eaffil .
  	?eaffilPart a:hasText ?eaffilName .
  }  
  
  OPTIONAL {
    ?ecountryPart a:isSubordinateTo ?editorPart .                 
    ?ecountryPart a:hasTag a:tag-ESWC--ecountry .
  	?ecountryPart a:hasText ?ecountryText .
  }
  
  	BIND (URI(?workshopUri) AS ?workshop)
  	BIND (URI(?vcountryText) AS ?vcountry)
    BIND (URI(CONCAT(?workshopUri, "#",?paperUri)) AS ?paper)
    BIND (URI(CONCAT(?workshopUri, "#proc")) AS ?proceedings)  
    BIND (URI(CONCAT(?workshopUri, "#conf")) AS ?conference)  
    
    BIND ( (xsd:dateTime(?startdate)) AS ?startdatedt)
    BIND ( (xsd:dateTime(?enddate)) AS ?enddatedt)
    
    BIND (URI(CONCAT("http://ceur-ws.org/","#", ENCODE_FOR_URI( replace(?authorName," ", "i") ) )) AS ?author) 
    BIND (URI(CONCAT("http://ceur-ws.org/","#", ENCODE_FOR_URI( replace(?editorName," ", "i") ) )) AS ?editor)
    BIND (URI(?ecountryText) AS ?ecountry)
                       
}